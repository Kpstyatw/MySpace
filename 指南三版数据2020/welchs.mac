################################################################################
#                                                                                           #
# Macro:   WELCHS.MAC                                                                      #
# Version: Release 13                                                                       #
# Date :   January 10, 2003  
# (revised to provide correct estimates when missing values are present)
#				
# Author:  Ken Kortge                                                                              #
#  Run this macro by entering %welchs command followed by 2 variables                                                                                         #   for example if c1 contains coefficients identifying the factor levels
#   and c2 contains a response, then enter %welchs c2 c1
#
#   Usage-  %welchs <response> <factor>  
#
#  Welch's ANOVA should be used when the variances are not equal and sample 
#  sizes  are different, especially when the group with the larger variance has 
#  the larger sample size.
#
#  The weights are used in calculation of the F-statistic.
#                                                                                           #
###############################################################################

macro

welchs y x
 
mcolumns  y  x  gvals gsize nis  vis  mis  wis  wmis  lambdai  fnumis

mcolumns  fnumis3 wyis2

mconstants  sumws sumwms  ystar lambda  F比  tval  P值 分子自由度 分母自由度

mconstants fnumis1 fnumis2 denom

noecho
note
note Macro is running ... please wait
mtitle "Welchs 方差分析F检验"
notitle
mreset

brief 0

stats y;
  by x;
  gvalues  gvals;
  n gsize;
  sums nis;
  variance vis;
  mean  mis.

divide gsize vis wis

sum  wis  sumws

multiply  wis  mis wmis

sum  wmis  sumwms

let  ystar = sumwms / sumws

let  lambdai = ((1 - (wis/sumws))**2) / (gsize - 1)

sum  lambdai lambda

count  mis  tval

let  wyis2 = wis * (mis**2)

sum wyis2  fnumis1

let fnumis2 = sumws*ystar**2

let fnumis3= fnumis1 - fnumis2

let  fnumis = fnumis3 / (tval - 1)

# test let denom = (1 + (2*(tval - 2)*lambda/(tval**2 - 1)))

let  F比  = fnumis / (1 + (2*(tval - 2)*lambda/(tval**2 - 1)))

let 分子自由度 = tval - 1

let 分母自由度 = (tval**2 - 1)/(3*lambda)

cdf  F比  P值;
   f   分子自由度  分母自由度.

let P值 = 1 - P值

brief 2
# test print ystar  lambda  fnumis1
# test print fnumis2 fnumis3
# test print fnumis denom


note i  n   均值 方差 权重
write gvals  gsize  mis   vis  wis

print   F比 分子自由度 分母自由度 P值
note

endmacro




