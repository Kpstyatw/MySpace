##############################################
# NCAP.MAC                                   #
# Produces Cnpk values for non-normal data   #
# Author: Bruce A. Gilbert                   #
# Date  : July, 1999                         #
# Version: Rel 12 for Windows                #
##############################################
      
macro

ncap  X USL LSL

MCONSTANT USL LSL N T1 T2 XMEDIAN XLOW XHIGH 
MCONSTANT CNP CNPK CNPL CNPU T995 T995A T005 T005A 
MCONSTANT XHIGH2 XLOW2 QHIGH QLOW TDIFFA TDIFFB
MCOLUMN X XTRIM XSORT XRANK XTILE

Sort X  X;
   By X.

LET XMEDIAN = MEDIAN(X)
LET N = COUNT(X)
LET T995 = .995*(N+1)
LET T005 = .005*(N+1)
LET T995A = ROUND(T995)
LET T005A = ROUND(T005)
LET TDIFFA = T995A - T995
LET TDIFFB = T005A - T005

IF T995 > N
	LET XHIGH = X(N)
	LET XHIGH2 = X(N-1)
	LET QHIGH = XHIGH - ABSO(TDIFFA)*(XHIGH - XHIGH2)
	LET CNPU = (USL-XMEDIAN)/(QHIGH - XMEDIAN)

	LET XLOW = X(1)
	LET XLOW2 = X(2)
	LET QLOW = XLOW + ABSO(TDIFFB)*(XLOW2 - XLOW)
	LET CNPL = (XMEDIAN - LSL)/(XMEDIAN - QLOW)

ELSEIF T995 = N
	LET QHIGH = X(N)
	LET CNPU = (USL - XMEDIAN)/(QHIGH - XMEDIAN)
	
	LET QLOW = X(1)
	LET CNPL = (XMEDIAN - LSL)/(XMEDIAN - QLOW)
ELSE

	IF TDIFFA > 0 		
					# WE ROUNDED UP SO NEED THAT VALUE PLUS THE ONE BELOW
		LET XHIGH = X(T995A)
		LET XHIGH2 = X(T995A - 1)
		LET QHIGH = XHIGH - ABSO(TDIFFA)*(XHIGH - XHIGH2)
		LET CNPU = (USL-XMEDIAN)/(QHIGH - XMEDIAN)

  	ELSEIF TDIFFA < 0

		LET XHIGH2 = X(T995A)
		LET XHIGH = X(T995A + 1)
		LET QHIGH = XHIGH2 + ABSO(TDIFFA)*(XHIGH - XHIGH2)
		LET CNPU = (USL-XMEDIAN)/(QHIGH - XMEDIAN)

	ELSE
	
		LET QHIGH = X(T995A)
		LET CNPU = (USL - XMEDIAN)/(QHIGH - XMEDIAN)

	ENDIF
	
	IF TDIFFB > 0
		
		LET XLOW2 = X(T005A)
		LET XLOW = X(T005A - 1)
		LET QLOW = XLOW2 - ABSO(TDIFFB)*(XLOW2 - XLOW)
		LET CNPL = (XMEDIAN - LSL)/(XMEDIAN - QLOW)
		
 	ELSEIF TDIFFB < 0

		LET XLOW = X(T005A)
		LET XLOW2 = X(T005A +1)
		LET QLOW = XLOW + ABSO(TDIFFB)*(XLOW2 - XLOW)
		LET CNPL = (XMEDIAN - LSL)/(XMEDIAN - QLOW)

	ELSE
		LET QLOW = X(T005A)
		LET CNPL = (XMEDIAN - LSL)/(XMEDIAN - QLOW)

	ENDIF
ENDIF
LET CNP = (USL - LSL)/(QHIGH - QLOW)
PRINT CNP
NOTE 非参数能力指数　CNP

LET CNPK = CNPU
PRINT CNPK
NOTE 上侧非参数能力指数　CNPU
LET CNPK = CNPL
PRINT CNPK
NOTE 下侧非参数能力指数　CNPL

LET CNPK = RMIN(CNPU, CNPL)
PRINT CNPK
NOTE 双侧非参数能力指数　CNPK 

endmacro
